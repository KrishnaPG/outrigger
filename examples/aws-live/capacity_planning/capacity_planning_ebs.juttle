// This program can be used to guide decisions on capacity planning
// for EBS volumes. It shows the average IO ops/second for all volumes
// (combined) and compares that to the configured total Iops capacity
// of the entire collection of volumes.
//
// If the total IO acticity is close to the Iops capacity, you may
// want to consider adding more volumes or faster volumes to spread
// the work across more items.
//
// The choice of 60 seconds when converting ops to io ops/second is
// inherently tied to the (currently) hardcoded 60 second window size
// used when computing metrics. If that changes, this 60 second
// divisor should change as well.
//
// This program assumes you are using an AWS integration and have
// stored events/metrics in a space called "aws".

(read -space 'aws' -last :1h: -source_type 'metric'
 aws_product='EBS'
 AND (metric_type='AWS CloudWatch' OR metric_type='AWS Metric')
 AND name='VolumeReadOps'
 OR name='VolumeWriteOps'
 | reduce avg_num_ops=avg(value) by item, name
 | reduce num_ops=sum(avg_num_ops) by item
 | put iops=num_ops/60.0
 | reduce Iops_Used=sum(iops)
 | keep Iops_Used;
 read -space 'aws' -last :1h: -source_type 'metric'
 aws_product='EBS'
 AND metric_type='AWS Aggregate'
 AND aggregate='EBS Volume Total Iops'
 | reduce Iops_Total=last(value)
)
    | join
    | put Iops_Headroom=Iops_Total-Iops_Used
    | keep Iops_Used, Iops_Headroom
    | split
    | @piechart -title "AWS Capacity Planning (Iops) for EBS";


