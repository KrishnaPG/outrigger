// This program can be used to show the value of any aggregate metric
// (EC2 instances, EBS volumes, total RDS storage capacity, etc.) by
// any aggregate metric supported by the AWS integration.
//
// Input controls allow you to choose an AWS product and aggregate
// metric and displays a timechart showing the value of that
// aggregate over time.
//
// This program assumes you are using an AWS integration and have
// stored events/metrics in a space called "aws".

import "aws_control_product.juttle" as control_product;
import "aws_control_aggregate.juttle" as control_agg;
import "aws_control_events.juttle" as control_events;

read -space 'aws' -last :6h: -source_type 'metric'
aws_product=control_product.aws_product_in
AND metric_type='AWS Aggregate'
AND aggregate=control_agg.aws_agg_in
    | @timechart -keyField "aggregate"
    -valueField "value"
    -title "AWS Totals (${control_agg.aws_agg_in})"
    -yScales.primary.label "Total"
    -display.markerSize 2
    -id 'agg_timechart';
read -space 'aws' -last :6h: -source_type 'event'
aws_product=control_product.aws_product_in
AND event_type =~ control_events.aws_show_events_in
    | put icon='fa-cloud'
    | @events -on 'agg_timechart'
    -nameField "event_type"
    -messageField "item"
    -display.typeField "icon"
