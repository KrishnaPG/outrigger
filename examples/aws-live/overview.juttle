(read aws product='EC2' OR product='EBS' OR product='RDS'
    | (filter product='EC2' AND metric_type='AWS Aggregate' AND aggregate='EC2 Instance Count'
       | view tile -title "Number of EC2 Instances";
       filter product='RDS' AND metric_type='AWS Aggregate' AND aggregate='RDS DB Total Allocated Storage'
       | view tile -title "Total DB Allocated Storage (GB)";
       filter product='EBS' AND metric_type='AWS Aggregate' AND aggregate='EBS Volume Total Iops'
       | view tile -title "Total EBS Volume Capacity (Iops)";
      );
 read cloudwatch -from :1 hour ago: -to :now: -period 3600 -statistics ['Maximum'] product='EC2' OR product='ELB' OR product='Lambda'
  | (filter
     // EC2: Any instances with failed status checks.
     (product='EC2' AND (name='StatusCheckFailed_Instance' OR name='StatusCheckFailed_System') AND value=1) OR
     // ELB: Any load balancers where UnHealthyHostCount is > 0
     (product='ELB' AND name='UnHealthyHostCount' AND value > 0) OR
     // Lambda: any function with errors > 0
     (product='Lambda' AND name='Errors' AND value > 0))
  | put oldtime=time
  | sort time by item
  | uniq item
  | put time=oldtime
  | keep time, product, name, value, item
  | ( reduce cnt=count()
      | put level=(cnt == 0 ? 'success' : 'error')
      | view tile -valueField "cnt" -title "Number of Problem Items (EC2, ELB, Lambda)" -levelField "level";
      view table -title "Items with problems" -columnOrder ["time", "product", "item", "name", "value"]
    )
)
